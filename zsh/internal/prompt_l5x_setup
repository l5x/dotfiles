#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <l5x.ionescu@gmail.com>
#   l5x
#
# Screenshots:
#   http://i.imgur.com/nrGV6pg.png
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_l5x_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_l5x_pwd=$(prompt-pwd)
}

function prompt_l5x_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_l5x_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{default}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{default}$%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{default}¿%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{default}❮%f%b'

  # _prompt_l5x_git=''
  _prompt_l5x_pwd=''

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{default}%n%f%F{default}@%f%F{default}%m%f "}%F{default}${_prompt_l5x_pwd}%(!. %B%F{default}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{default}'
  RPROMPT+=${show_return}
  RPROMPT+='%f)${VIM:+" %B%F{default}V%f%b"}'
  SPROMPT='zsh: correct %F{default}%R%f to %F{default}%r%f [nyae]? '
}

function prompt_l5x_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'l5x'
}

prompt_l5x_setup "$@"
# vim: ft=zsh
